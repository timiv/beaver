%rules

    Spec
        = ParserSpec? ScannerSpec?
        ;

    ParserSpec
        = "%rules" Rule+ ["%precedence" Precedence+]
        ;

    Rule
        = NAME "=" AltList ";"
        ;

    AltList
        = Alt
        | AltList "|" Alt
        ;

    Alt
        = ["{" NAME "}"] Item*
        ;

    Item
        = { Static } TEXT
        | { Symbol } [ref:NAME ":"] NAME OPER?
        | { Inline } "[" Item+ "]"
        ;

    Precedence
        = PrecItemList ":" ASSOC ";"
        ;

    PrecItemList
        = PrecItem
        | PrecItemList "," PrecItem
        ;

    PrecItem
        = { Term } TEXT
        | { Rule } NAME
        ;

    ScannerSpec
        = ["%macros" macros:MacroDecl+] "%tokens" terminals:TermDecl+ states:ScannerState*
        ;

    MacroDecl
        = NAME "=" RegExp ";"
        ;

    TermDecl
        = NAME "=" RegExp ["/" ctx:RegExp] ["->" event:NAME] ";"
        ;

    ScannerState
        = selector:STSEL NAME terminals:TermDecl+
        ;

    RegExp
        = RegExpItem+
        | RegExp "|" RegExpItem+
        ;

    RegExpItem
        =           CharExpr
        | { Close } CharExpr OPER
        | { Multi } CharExpr Multiplicity
        ;

    Multiplicity
        = "{" min:NUM "}"
        | "{" min:NUM "," max:NUM? "}"
        ;

    CharExpr
        = { Text   } TEXT
        | { Range  } RangeExpr
        | { Nested } "(" RegExp ")"
        ;

    RangeExpr
        = { Range } RANGE
        | { Macro } macro:NAME
        | { Minus } diff:RangeExpr "\\" range:RangeExpr
        ;

%precedence

    RangeExprMinus : left;

%macros

    any     = [^]
            ;
    dot     = any \ $
            ;
    esc     = dot \ [\\]
            ;
    digit   = [0-9]
            ;
    letter  = [a-zA-Z_]
            ;

%tokens

    WS      = [ \t]         -> skip
            ;
    MACROS  = "%macros"     -> ScannerSpecStart
            ;
    TOKENS  = "%tokens"     -> ScannerSpecStart
            ;
    STSEL   = "@" | "^"
            ;
    OPER    = "?" | "*" | "+"
            ;
    NUM     = digit+
            ;
    ASSOC   = "left" | "right" | "none"
            ;
    NAME    = letter ( letter | digit )* | "$"
            ;
    TEXT    = "\"" ( esc \ ["] | "\\" dot )* "\""
            ;
            
@SCANNER_SPEC

    RANGE   = "[" "^"? ( esc \ [\]] | "\\" dot )* "]"
            ;
